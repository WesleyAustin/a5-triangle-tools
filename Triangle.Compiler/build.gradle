apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

java {
    sourceCompatibility = 21
}

repositories {
    mavenCentral()
}


dependencies {
	implementation project(':Triangle.AbstractMachine')
    implementation group: 'com.github.spullara.cli-parser', name: 'cli-parser', version: '1.1.5'
	testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

application {
	mainClass = 'Triangle.Compiler'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn project(':Triangle.AbstractMachine').tasks.named('jar')
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
// allow access to programs for unit tests
sourceSets.test.resources.srcDir file("$rootDir/programs")

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
